
repositories {
  mavenCentral()
  maven { url ossSnapshotRepositoryUrl }
}

if (!project.version.endsWith('-SNAPSHOT')) {
  apply plugin: 'signing'
  signing {
    sign publishing.publications.maven
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      
      pom {
        name = pomProjectTitle
        packaging = 'jar'
        description = pomProjectDescription
        url = "https://github.com/aestasit/${githubProjectName}"
        
        scm {
          url = "scm:git@github.com:aestasit/${githubProjectName}.git"
          connection = "scm:git@github.com:aestasit/${githubProjectName}.git"
          developerConnection = "scm:git@github.com:aestasit/${githubProjectName}.git"
        }
        
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }
        
        developers {
          developer {
            id = 'aadamovich'
            name = 'Andrey Adamovich'
          }
        }
      }
    }
  }
  
  repositories {
    maven {
      if (project.version.endsWith('-SNAPSHOT')) {
        url = ossSnapshotRepositoryUrl
        credentials {
          username = ossSnapshotRepositoryUserName
          password = ossSnapshotRepositoryPassword
        }
      } else {
        url = ossReleaseRepositoryUrl
        credentials {
          username = ossReleaseRepositoryUserName
          password = ossReleaseRepositoryPassword
        }
      }
    }
  }
}
